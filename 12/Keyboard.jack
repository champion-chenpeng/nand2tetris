// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int keyboard;

    /** Initializes the keyboard. */
    function void init() {
        let keyboard = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(keyboard);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key;
        // display cursor
        do Output.printChar(0);

        // wait for a key
        while (Keyboard.keyPressed() = 0) {
        }
        let key = Keyboard.keyPressed();

        // erase cursor
        do Output.backSpace();

        if ((key > 31) & (key < 127)) {
            do Output.printChar(key);
        }
        if (key = String.newLine()) {
            do Output.println();
        }
        if (key = String.backSpace()) {
            do Output.backSpace();
            do Output.printChar(32);
            do Output.backSpace();
        }
        
        // wait for release
        while (~(Keyboard.keyPressed() = 0)) {
        }
        return key;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var char currentChar;
        var String currentString;

        let currentString = String.new(100); // may vary according to need

        do Output.printString(message);
        let currentChar = Keyboard.readChar();
        while (~(currentChar = String.newLine())) {
            if (currentChar = String.backSpace()) {
                do currentString.eraseLastChar();
            } else {
                do currentString.appendChar(currentChar);
            }
            let currentChar = Keyboard.readChar();
        }
        return currentString;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String intStr;
        let intStr = Keyboard.readLine(message);
        return intStr.intValue();
    }
}
